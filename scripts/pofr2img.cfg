# Plot the p(r) function, GIFT *.POR input expected
# Optimized for gnuplot 4.4

set datafile commentschars "#!%ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz"
plot inputfile using 1:2 with points  # To compute r and P(r) range

set terminal pngcairo size 437, 437 truecolor enhanced # transparent
set output outputfile

#set xrange [0:nn_dmax/10.0]

if (exists("Rmax"))  set xrange [0:Rmax];  else  set xrange [0:]
if (exists("first"))  first = first - 1;  else  first = 0
set yrange [0: GPVAL_DATA_Y_MAX]

dx = real(sprintf("%.0f", GPVAL_DATA_X_MAX/4.0))
set xtics border offset -0.3,0 nomirror font "arial, 12" 0, dx
set ytics format "" nomirror 0, GPVAL_DATA_Y_MAX/4.0

set style line 1             lc rgb "#e1edff"  # light blue fill
set style line 2             lc rgb "#c4dbff"  # blue fill
set style line 3             lc rgb "#ffb4b5"  # red fill
set style line 4 linewidth 1 lc rgb "#3768b8"  # blue line
set style line 5 linewidth 2 lc rgb "#e41a1c"  # red line
set style line 7 lt 0 linecolor rgb "#ed6869" linewidth 3    # vertical gridlines

if (!exists("skip"))  skip = 0

set bmargin 2
set tmargin 0
set lmargin 0.4
set rmargin 0
unset key
set border 3 linecolor rgb "#9bb3db"

set bars small
set tics front
set label 1 sprintf("D_{max} = %.1f nm", nn_dmax/10.0) at nn_dmax/10.0,GPVAL_DATA_Y_MAX/1.05 right \
                   font "arial, 12" textcolor "#e41a1c" front

if (exists("nn_dmax"))  set arrow from nn_dmax/10.0, graph 0 to nn_dmax/10.0, graph 1 nohead ls 7 front

plot inputfile using 1:($2 < 0    ? $2  : 1/0) index 0 every ::first w filledcurves y1=0 linestyle 3, \
            '' using 1:2                       index 0 every ::first w filledcurves y1=0 linestyle 1, \
            '' using 1:2                       index 0 every ::first w lines             linestyle 4, \
            '' using 1:($2 < 0    ? $2  : 1/0) index 0 every ::first w lines             linestyle 5
